#!/usr/bin/awk -f
# Extract IRC statistics from Weechat logs for various things.
#
# Usage (include one of the following parameters):
#   blamereport showkicks=true showblames=true penovwhiteinge=true \
#       showchatty=true showjoinsparts=true showemotes=true

{
    IGNORECASE = 1

    if (NR == 1) {
        begindate = $1
    }
}

$0 ~ /hobo knife/ {
    knife += 1
}

# Collect chatty stats
$3 ~ /[a-zA-Z@+_-]+/ {
    sub(/@/, "", $3)
    sub(/+/, "", $3)
    chatty[$3] += 1
}

# Collect joins
$6 == "has" && $7 == "joined" {
    joins_parts[$4] += 1
    joins[$4] = $1
}

# Collect parts
$6 == "has" && $7 == "quit" {
    joins_parts[$4] += 1
    quits[$4] = $1
}

# Collect peno vs. whiteinge stats
$3 == "*" && $4 == "peno" && $6 ~ /wh[i1]t[e3][i1]ng[e3]/ {
    action=$4 " " $5 " whiteinge"

    for (i=7; i<=NF; i++) {
        action = action " " $i
    }

    penoactions[action] += 1
}

# Collect blame stats
$3 == "*" && $5 == "blames" {
    blames += 1
    blamers[$4] += 1
    blamees[$6] += 1
}

# Collect emote stats
$3 == "*" {
    emotes[$4] += 1
}

# Collect kick stats
$3 == "<--" && $6 == "kicked" {
    kicks += 1
    kickers[$4] += 1
    kickees[$7] += 1

    reason = ""
    for (i=8; i<=NF; i++) {
        reason = reason " " $i
    }
    kick_reasons[reason] += 1
}

END {
    ### blames
    if (showblames == "true") {
        for (i in blamers) num_blamers++
        for (i in blamees) num_blamees++

        print "\nThere has been", blames, "aspersions cast",
            "about", num_blamees, "innocent souls",
            "by", num_blamers, "vicious haters",
            "since", begindate "."

        print "\nTop 5 blamers:"

        command = "sort -r -n -k4 | head -5"

        for (person in blamers) print "\t" person, "cast blame", blamers[person],
            "times" | command
        close(command)

        print "\nTop 5 blamees:"

        for (person in blamees) print "\t" person, "was blamed", blamees[person],
            "times" | command
        close(command)
    }

    ### kicks
    if (showkicks == "true") {
        for (i in kickers) num_kickers++
        for (i in kickees) num_kickees++

        print "\nThere has been", kicks, "forceful explusions from our fair channel",
            "of", num_kickees, "innocent victims",
            "by", num_kickers, "wrathful bullies",
            "since", begindate "."

        print "\nTop 5 kickers:"

        command = "sort -r -n -k4 | head -5"

        for (person in kickers) print "\t" person, "has kicked", kickers[person],
            "times" | command
        close(command)

        print "\nTop 5 kickees:"

        for (person in kickees) print "\t" person, "was kicked", kickees[person],
            "times" | command
        close(command)

        if (showkickreasons == "true") {
            print "\nReasons for kicking:"
            for (reason in kick_reasons) print "\t" reason
        }
    }

    ### penoactions
    if (penovwhiteinge == "true") {
        print "\npeno has done the following horrible things to whiteinge:"

        penocommand = "sort -r -n -k4"

        for (action in penoactions) print "\t" action, penoactions[action],
            "times" | penocommand
        close(penocommand)
    }

    ### chattyness
    if (showchatty == "true") {
        print "\nThe five most talkative people since", begindate, "are:\n"

        chattycommand = "sort -r -n -k4 | head -5"
        for (person in chatty) print "\t" person, "spoke up", chatty[person],
             "times" | chattycommand
        close(chattycommand)
    }

    ### joins/parts
    if (showjoinsparts == "true") {
        print "\nThe five people who have joined/parted most often since",
              begindate, "are:\n"

        joinpartcommand = "sort -r -n -k3 | head -5"
        for (person in joins_parts) print "\t" person, "joined/parted",
             joins_parts[person], "times" | joinpartcommand
        close(joinpartcommand)
    }

    ### emotes
    if (showemotes == "true") {
        print "\nThe five people who have emoted most often since",
              begindate, "are:\n"

        emotecommand = "sort -r -n -k3 | head -5"
        for (person in emotes) print "\t" person, "emoted",
             emotes[person], "times" | emotecommand
        close(emotecommand)
    }

    ### hobo knife
    if (showknife == "true") {
        print "\nThere have been", knife, "hobo-knife wielding individuals since",
              begindate, "\n"

        # emotecommand = "sort -r -n -k3 | head -5"
        # for (person in emotes) print "\t" person, "emoted",
        #      emotes[person], "times" | emotecommand
        # close(emotecommand)
    }
}
