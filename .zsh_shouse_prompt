# Best Goddamn zsh prompt in the whole world.
# Author: Seth House <seth@eseth.com>
# thanks to Phil of phil's prompt: http://aperiodic.net/phil/prompt/
# and Frank Terbeck <ft@bewatermyfriend.org> for the VCS_INFO work

fpath=( ~/.zsh_functions $fpath )

autoload -U colors
autoload -U promptinit
autoload -Uz vcs_info

colors
zstyle ':vcs_info:*' enable hg git bzr svn
zstyle ':vcs_info:(hg|git):*' get-revision true
zstyle ':vcs_info:(hg|git):*' check-for-changes true
zstyle ':vcs_info:hg:*' get-bookmarks true
zstyle ':vcs_info:hg:*' unstagedstr "$PR_BGREEN+$PR_BBLACK"
zstyle ':vcs_info:hg:*' hgrevformat "%r" # only show local rev.
zstyle ':vcs_info:hg:*' branchformat "%b" # only show branch
zstyle ':vcs_info:*' formats '(%s)[%i%u %b %m]' # rev, changes, branch, misc


# set some color shortcuts
for COLOR in RED GREEN YELLOW WHITE BLACK CYAN; do
    eval PR_$COLOR='%{$fg[${(L)COLOR}]%}'         
    eval PR_B$COLOR='%{$fg_bold[${(L)COLOR}]%}'
done                                                 
PR_RESET="%{${reset_color}%}";    


# Executed before each prompt
function precmd {
    vcs_info
    setprompt
    venv_rprompt
}


# Executed after a command has been read and is to be executed.
function preexec {
    # if running gnu screen, set the window name to the last run command
    # FIXME any way to make this not change certain window titles (by window
    # number or if a title isn't already set?)
    if [[ "$TERM" =~ "screen" ]]; then
        local CMD=${1[(wr)^(*=*|ssh|sudo|-*)]}
        echo -ne "\ek$CMD\e\\"
    fi
}

# Encapsulate building the prompt.
function setprompt {
    # display in yellow if the current dir isn not writable
    local pr_pwd="$PR_BGREEN"$(test -w $PWD || print "$PR_BYELLOW")"%~$PR_RESET"

    # show job count
    local pr_jobs="$PR_BBLACK%j$PR_RESET"

    # display the hostname only if ssh-ed into another box
    local pr_user="$PR_BBLACK<$PR_RESET%n"$(test -z $SSH_CLIENT || print '@%m')"$PR_BBLACK>$PR_RESET"

    # make the prompt red if the last command exited unsuccessfully
    local pr_prompt="%(0?..$PR_BRED)%#$PR_RESET "

    # display vcs information for the current dir (if any)
    if [ -n "$vcs_info_msg_0_" ]; then
        local pr_vcs="$PR_BBLACK$vcs_info_msg_0_$PR_RESET"
    fi

    # FIXME: is there a way to get the width from the real variables without
    # the color escapes? repeating the prompt twice sucks.
    if [[ -z $SSH_CLIENT ]]; then
        local width=$((${#${(%):-<%n> %~ }} + ${#${(%):-$vcs_info_msg_0_}} + 1))
    else
        local width=$((${#${(%):-<%n@%m> %~ }} + ${#${(%):-$vcs_info_msg_0_}} + 1))
    fi

    # RANDOM will not change unless referenced in parent shell
    echo $RANDOM &>/dev/null
    local pr_pet=$(pet_wander $width)

    PROMPT="$pr_user $pr_pwd $pr_vcs $pr_pet"$'\n'"$pr_jobs $pr_prompt"
}


# A domestic animal, the _tame dog_ (_Canis familiaris_)
pet_wander () {
    # Draw enough padding to reach the end of the window;
    # pet likes to wander this space

    local pet="d"

    if [[ $[${RANDOM}%20] == 0 ]]; then
        pet="^"
    elif [[ $[${RANDOM}%50] == 0 ]]; then
        pet="D"
    fi

    local width=$1
    local padding=$(($COLUMNS - $width - 1))
    local position=$[${RANDOM}%$padding]
    local left=$((padding - position))

    local pr_pet="%{$bg_bold[white]$fg[black]%}$pet%{$reset_color%}"

    echo -ne "%{$fg_bold[black]%}"
    echo -ne "${(l/$left//./)}"
    echo -ne "$pr_pet"
    echo -ne "%{$fg_bold[black]%}"
    echo -ne "${(r/$position//./)}"
    echo -ne "%{$reset_color%}"
}

venv_rprompt () {
    if [[ -n $VIRTUAL_ENV ]]; then
        RPROMPT="$PR_BBLACK venv:$(basename $VIRTUAL_ENV)$PR_RESET"
    else
        RPROMPT=""
    fi
}

# function zle-keymap-select {
    # VIMODE="${${KEYMAP/vicmd/ M:command}/(main|viins)/}"
    # zle reset-prompt
# }
# zle -N zle-keymap-select
#
########## Or this one?:
# function zle-line-init zle-keymap-select {
    # RPS1="${${KEYMAP/vicmd/-- NORMAL --}/(main|viins)/-- INSERT --}"
    # RPS2=$RPS1
    # zle reset-prompt
# }
# zle -N zle-line-init
# zle -N zle-keymap-select
